lexfile     =   aliases "Delimiter" definitions .
aliases     =   { alias } .
alias       =   "Identifier" RegularExpression .
definitions =   { definition } .
definition  =   "Identifier" RegularExpression .

RegularExpression   =   ?union?     Alternative { "Alternation" Alternative } .
Alternative         =   ?concat?    Kleen { Kleen } .
Kleen               =   ?kleen?     Term [ "Kleen" ] .
Term                =   ?match?     "LMatch" RegularExpression "RMatch"
                    |   ?group?     "LGroup" RegularExpression "RGroup"
                    |   ?chargroup? "LCharGroup" { CharTerm } [ "CharNeg" CharTerm { CharTerm } ] "RCharGroup"
                    |   ?alias?     "Alias"
                    |   ?char?      "CharRange"
                    |   ?char?      "CharNeg"
                    |   ?char?      "Char" .
CharTerm            =   ?charset?   "Char" "CharRange" "Char"
                    |   ?char?      "Char" .